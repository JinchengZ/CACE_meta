model{
  for (i in 1:I) {
  
R0_4[i, ] ~ dmulti(prob0[i, ], N0[i])
prob0[i, 1] <- pi_n[i]*(1-s1[i]) + pi_c[i]*(1-v1[i])
prob0[i, 2] <- pi_n[i]*s1[i] + pi_c[i]*v1[i]
prob0[i, 3] <- pi_a[i]*(1-b1[i])
prob0[i, 4] <- pi_a[i]*b1[i]

R1_4[i, ] ~ dmulti(prob1[i, ], N1[i])
prob1[i, 1] <- pi_n[i]*(1-s1[i])
prob1[i, 2] <- pi_n[i]*s1[i]
prob1[i, 3] <- pi_c[i]*(1-u1[i])+pi_a[i]*(1-b1[i])
prob1[i, 4] <- pi_c[i]*u1[i]+pi_a[i]*b1[i]
  
  probit(u1[i]) <- alpha_u + delta_u[i]
  probit(v1[i]) <- alpha_v + delta_v[i]
  delta_v[i] ~ dnorm(0, tau_v)
  delta_u[i] ~ dnorm(0, tau_u)
  
  delta_Z[i, 1:2] ~ dmnorm(c(0, 0), Omega_Z[ , ])
  z1[i] <- alpha_z1 + delta_Z[i, 1]
  z2[i] <- alpha_z2 + delta_Z[i, 2]
  
  pi_n[i] <- exp(z1[i])/(1+exp(z1[i])+exp(z2[i]))
  pi_a[i] <- exp(z2[i])/(1+exp(z1[i])+exp(z2[i]))
  pi_c[i] <- 1-pi_a[i]-pi_n[i]
  
  logit(s1[i]) <- alpha_s1 + delta_s[i]
  logit(b1[i]) <- alpha_b1 + delta_b[i]
  delta_s[i] ~ dnorm(0, tau_s)
  delta_b[i] ~ dnorm(0, tau_b)
  } 
  
  pin <- exp(alpha_z1)/(1+exp(alpha_z1)+exp(alpha_z2))
  pia <- exp(alpha_z2)/(1+exp(alpha_z1)+exp(alpha_z2))
  pic <- 1-pia-pin
  u1out <- phi(alpha_u/sqrt(1+sigma_u^2))
  v1out <- phi(alpha_v/sqrt(1+sigma_v^2))
  s1out <- ilogit(alpha_s1)
  b1out <- ilogit(alpha_b1)

  CACE <- phi(alpha_u/sqrt(1+sigma_u^2))-phi(alpha_v/sqrt(1+sigma_v^2))
    
  # priors
  alpha_z1 ~  dnorm(0, 0.16)
  alpha_z2 ~ dnorm(0, 0.16)
#  tau_z1 ~ dgamma(2, 2)
#  sigma_z1 <- 1/sqrt(tau_z1)
  Omega_Z[1:2, 1:2] ~ dwish(II[,], 3)
  Sigma_Z[1:2, 1:2] <- inverse(Omega_Z[, ])
  
  alpha_s1 ~  dnorm(0, 0.25)
  tau_s ~ dgamma(2, 2)
  sigma_s <- 1/sqrt(tau_s)
  alpha_b1 ~  dnorm(0, 0.25)
  tau_b ~ dgamma(2, 2)
  sigma_b <- 1/sqrt(tau_b)
  
  alpha_v ~  dnorm(0, 0.25)
  tau_v ~ dgamma(2, 2)
  sigma_v <- 1/sqrt(tau_v)
  alpha_u ~  dnorm(0, 0.25)
  tau_u ~ dgamma(2, 2)
  sigma_u <- 1/sqrt(tau_u)
  }
